// Apply to all modules
allprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'
  group = 'com.github.drepic26.couponcodes'

  version = '3.0'
  project.ext.author = 'Drepic26'
}

dependencies {
  compile project(':core')
  compile project(':mods:sponge')
  compile project(':mods:bukkit')
}

project(':mods:sponge') {
  repositories {
    mavenCentral()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/Sponge/maven'
    }
  }
  dependencies {
    compile 'org.spongepowered:spongeapi:1.0'
    compile project(':core')
  }
}

project(':core') {
  processResources {
    from(sourceSets.main.resources.srcDirs) {
      include 'couponcodes.properties'
      expand project.properties
    }
    from(sourceSets.main.resources.srcDirs) {
      exclude 'couponcodes.properties'
    }
  }
}

project(':mods:bukkit') {
  repositories {
    mavenCentral()
    maven {
      name 'Bukkit maven repo'
      url 'http://repo.bukkit.org/content/groups/public/'
    }
	maven {
	  name 'vault-repo'
	  url 'http://nexus.theyeticave.net/content/repositories/pub_releases'
	}
	maven{
	  name 'Spigot maven repo'
	  url 'https://hub.spigotmc.org/nexus/content/groups/public/'
	}
  }

  processResources {
    from(sourceSets.main.resources.srcDirs) {
      include 'plugin.yml'
      expand project.properties
    }
    from(sourceSets.main.resources.srcDirs) {
      exclude 'plugin.yml'
    }
  }

  dependencies {
    compile 'org.bukkit:bukkit:1.8-R0.1-SNAPSHOT'
	compile 'net.milkbowl.vault:VaultAPI:1.5'
    compile project(':core')
  }
}

task spongeJar(type: Jar) {
  from { project(':core').sourceSets.main.output }
  from { project(':mods:sponge').sourceSets.main.output }
  classifier = 'Sponge'
}

task bukkitJar(type: Jar) {
  from { project(':core').sourceSets.main.output }
  from { project(':mods:bukkit').sourceSets.main.output }
  classifier = 'Bukkit'
}

task universalJar(type: Jar) {
  from { project(':core').sourceSets.main.output }
  from { project(':mods:sponge').sourceSets.main.output }
  from { project(':mods:bukkit').sourceSets.main.output }
  classifier = 'Universal'
}

artifacts {
  archives spongeJar
  archives bukkitJar
  archives universalJar
}
