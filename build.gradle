allprojects {
  apply plugin: 'java'
  apply plugin: 'eclipse'

  group = 'com.github.drepic26'
  archivesBaseName = "couponcodes"
  version = '3.0'
  project.ext.author = 'Drepic26'

  repositories {
    mavenCentral()
    maven {
        name 'Sponge maven repo'
        url 'http://repo.spongepowered.org/maven'
    }
    maven {
      name 'Bukkit maven repo'
      url 'http://repo.bukkit.org/content/groups/public/'
    }
    maven {
      name 'vault-repo'
      url 'http://nexus.theyeticave.net/content/repositories/pub_releases'
    }
    maven{
      name 'Spigot maven repo'
      url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
    maven {
      name 'Plugin Metrics'
      url 'http://repo.mcstats.org/content/repositories/public'
    }
  }
}

apply plugin: 'signing'
apply plugin: 'maven'

dependencies {
  compile project(':api')
  compile project(':core')
  compile project(':mods:sponge')
  compile project(':mods:bukkit')
}

project(':api') {
}

project(':core') {
  dependencies {
    compile project(':api')
  }
  def filteredSourceDir = file("${buildDir}/filtered")
  sourceSets {
    filtered {
        java {
            srcDirs = [filteredSourceDir]
        }
    }
  }
  compileJava.source = sourceSets.filtered.java
  task processVersion (type: Copy) {
    from sourceSets.main.java
    into filteredSourceDir
    expand(version: project.version)
  }
  compileJava.dependsOn processVersion
}

project(':mods:sponge') {
  dependencies {
    compile 'org.spongepowered:spongeapi:1.0'
    compile project(':core')
  }
}

project(':mods:bukkit') {
  dependencies {
    compile 'org.bukkit:bukkit:1.8-R0.1-SNAPSHOT'
    compile 'net.milkbowl.vault:VaultAPI:1.5'
    compile project(':core')
  }
  processResources {
    from(sourceSets.main.resources.srcDirs) {
      include 'plugin.yml'
      expand project.properties
    }
    from(sourceSets.main.resources.srcDirs) {
      exclude 'plugin.yml'
    }
  }
}

jar {
  from { project(":api").sourceSets.main.output }
  from { project(":core").sourceSets.main.output }
  from { project(":mods:bukkit").sourceSets.main.output }
  from { project(":mods:sponge").sourceSets.main.output }
}

task sourcesJar(type: Jar) {
  from project(":api").sourceSets.main.java
  from project(":core").sourceSets.main.java
  from project(":mods:bukkit").sourceSets.main.java
  from project(":mods:sponge").sourceSets.main.java
  classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  classifier = "javadoc"
  dependsOn ":api:javadoc"
  dependsOn ":core:javadoc"
  dependsOn ":mods:bukkit:javadoc"
  dependsOn ":mods:sponge:javadoc"
}

javadoc {
  source = [
  project(":api").sourceSets.main.java,
  project(":core").sourceSets.main.java,
  project(":mods:bukkit").sourceSets.main.java,
  project(":mods:sponge").sourceSets.main.java,
  ]
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

signing {
  if (project.hasProperty("ossrhUsername")) {
    sign configurations.archives
  }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      if (project.hasProperty("ossrhUsername")) {
        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }
      }

      pom.project {
        name 'CouponCodes'
        packaging 'jar'
        description 'A bukkit plugin for coupons'
        url 'https://github.com/Drepic26/CouponCodes3'

        scm {
          url 'scm:git@github.com:Drepic26/CouponCodes3.git'
          connection 'scm:git@github.com:Drepic26/CouponCodes3.git'
          developerConnection 'scm:git@github.com:Drepic26/CouponCodes3.git'
        }

        licenses {
          license {
            name 'MIT license'
            url 'http://opensource.org/licenses/MIT'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'drepic26'
            name 'Drepic26'
          }
        }
      }
    }
  }
}
